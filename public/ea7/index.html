<!DOCTYPE html>
<html lang="de">

<head>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width,initial-scale=1">
    <title>EA 7 | Z-Buffer Visualisierung | Pascal Hürten</title>
    <link rel="stylesheet" href="../style.css">
    <link rel="stylesheet" href="../ea2/style.css">
    <style>
        .math-formula {
            background: #f5f5f5;
            padding: 12px;
            margin: 10px 0;
            border-left: 4px solid #0066cc;
            font-family: 'Courier New', monospace;
            font-size: 13px;
            overflow-x: auto;
        }

        .shape-section {
            margin: 20px 0;
            padding: 15px;
            border: 1px solid #ddd;
            border-radius: 4px;
        }

        .shape-section h4 {
            margin-top: 0;
            color: #0066cc;
        }
    </style>

    <!-- libraries & geometry generators -->
    <script src="../util/ext/gl-matrix.js"></script>
    <script src="../util/models/VertexDataGenerator.js"></script>
    <script src="../util/models/sphere.js"></script>
    <script src="../util/models/cube.js"></script>
    <script src="../util/models/cone.js"></script>
    <script src="../util/models/torus.js"></script>
    <script src="../util/models/pine.js"></script>
    <script src="../util/models/tree.js"></script>
    <script src="../util/models/bush.js"></script>
    <script src="../util/models/cloud.js"></script>
    <script src="../util/models/skydome.js"></script>
    <script src="../util/models/grass.js"></script>
    <script src="../util/models/plane.js"></script>
    <script src="../util/models/Model.js"></script>
    <script src="../util/models/ModelAnimator.js"></script>
    <!-- main app -->
    <script src="main.js"></script>

    <!-- Shaders (app reads these by id at init) -->
    <script type="x-shader/x-vertex" id="vertexshader">#version 300 es
in vec3 aPosition;
in vec3 aNormal;
in vec3 aColor; // optional per-vertex color

uniform mat4 uPMatrix;
uniform mat4 uMVMatrix;
uniform mat3 uNormalMatrix;
uniform float uCurveStart;
uniform float uCurveStrength;
uniform float uCurveExponent;
uniform float uCurveRadius; // Earth-like curvature radius (same units as scene)
uniform bool uIsSky; // sky pass flag
uniform float uSkyRadius; // for gradient calculation if needed
uniform float uWindTime; // time for wind animation
uniform float uWindStrength; // wind sway strength
uniform vec2 uWindDirection; // wind direction XZ
uniform float uWindModelStrength; // per-model wind multiplier

out vec3 vNormal;
out vec3 vPosition;
out vec3 vColor;

void main(){
	// Apply wind sway in local space before view transform
	vec3 pos = aPosition;
	// Sway amount increases quadratically with vertex height (Y coordinate)
	// This makes grass bend naturally with tops moving much more than base
	float heightFactor = pos.y * pos.y; // Quadratic for natural bending
	float swayAmount = heightFactor * uWindStrength * uWindModelStrength;
	float wave = sin(uWindTime * 1.5 + pos.x * 0.5 + pos.z * 0.5) * 0.5 + 
	             sin(uWindTime * 2.3 + pos.x * 0.3) * 0.3;
	pos.x += uWindDirection.x * wave * swayAmount;
	pos.z += uWindDirection.y * wave * swayAmount;
	
	// compute position in view (model-view) space
	vec4 mvPos = uMVMatrix * vec4(pos, 1.0);

    // Earth-like curvature: quadratic sagitta drop based on horizontal distance
    // Use view-space horizontal distance (XZ) and a sphere radius.
    if (!uIsSky) {
        float d = length(mvPos.xz);
        // Ease-in the effect to avoid bending right at the camera
        float tCurve = smoothstep(uCurveStart, uCurveStart * 2.0, d);
        tCurve = pow(tCurve, max(0.0001, uCurveExponent));
        // Sagitta: drop = R - sqrt(R^2 - d^2)  (approx ~ d^2/(2R) for small d)
        float R = max(0.0001, uCurveRadius);
        float inside = max(0.0, R * R - d * d);
        float drop = (R - sqrt(inside));
        mvPos.y -= drop * uCurveStrength * tCurve;
    }
    gl_Position = uPMatrix * mvPos;
	// pass normal and position to fragment shader
	vNormal = normalize(uNormalMatrix * aNormal);
	vPosition = mvPos.xyz;
    vColor = aColor;
}
</script>
    <script type="x-shader/x-fragment" id="fragmentshader">#version 300 es
precision mediump float;
in vec3 vNormal;
in vec3 vPosition;
in vec3 vColor;

uniform vec3 uModelColor;
uniform bool uUseVertexColor;
uniform vec3 uLightDirection;
uniform float uAmbientStrength;
uniform float uShadowStrength; // 0..1 how strong shadows get
uniform float uShadowExponent; // controls falloff of shadow
uniform vec3  uFogColor;           // target color to blend towards
uniform float uFogNear;            // start fog distance
uniform float uFogStrength;        // 0..1 overall fog influence
uniform float uFogDensity;         // exponential fog density
uniform bool uIsSky;               // sky pass flag
uniform float uSkyRadius;          // skydome radius
uniform float uSkyHazeStrength;    // 0..1 how much horizon haze
uniform float uSkyDesatStrength;   // 0..1 desaturation near horizon
uniform bool uWireframePass;       // when true, always render black lines
uniform bool uIsGround;            // flag for ground plane special treatment

out vec4 fragColor;

void main() {
    // Force wireframe to solid black, no lighting/fog
    if (uWireframePass) {
        fragColor = vec4(0.0, 0.0, 0.0, 1.0);
        return;
    }
    if (uIsSky) {
        // Simple atmospheric gradient: use normalized height in view space
        float h = clamp((vPosition.y + uSkyRadius * 0.2) / (uSkyRadius * 1.2), 0.0, 1.0);
        // Controlled haze towards fog color near horizon
        float haze = uSkyHazeStrength * smoothstep(0.0, 0.35, 1.0 - h);
        vec3 hazeColor = mix(vColor, uFogColor, haze);
        // Controlled desaturation at lower altitudes
        float desat = uSkyDesatStrength * (1.0 - h);
        float gray = dot(hazeColor, vec3(0.299,0.587,0.114));
        vec3 finalCol = mix(hazeColor, vec3(gray), desat);
        fragColor = vec4(finalCol, 1.0);
        return;
    }
	vec3 N = normalize(vNormal);
	vec3 L = normalize(uLightDirection);
	float diffuse = max(dot(N, -L), 0.0);

	// basic lighting (ambient + diffuse)
	float lighting = uAmbientStrength + diffuse * (1.0 - uAmbientStrength);

	// shadow factor: stronger when diffuse is small
	float shadowFactor = pow(1.0 - diffuse, uShadowExponent);
	float shadow = clamp(uShadowStrength * shadowFactor, 0.0, 1.0);

	// base color: either per-vertex or uniform
    vec3 baseColor = uUseVertexColor ? vColor : uModelColor;
    // lit color
    vec3 litColor = baseColor * lighting;

	// dark base for shadows (very low ambient in deep shadow)
    vec3 shadowColor = baseColor * (uAmbientStrength * 0.25);

	// blend between shadowColor and litColor depending on shadow
    vec3 color = mix(litColor, shadowColor, shadow);

    // Depth-based desaturation using view-space depth (vPosition.z)
    float depth = abs(vPosition.z);
    float d = max(0.0, depth - uFogNear);
    float desatAmount = uFogStrength * (1.0 - exp(-uFogDensity * d));
    // Compute luminance (perceived grayscale)
    float gray = dot(color, vec3(0.299, 0.587, 0.114));
    vec3 desatColor = mix(color, vec3(gray), desatAmount);
    
    // Near-camera saturation boost for more vivid foreground
    float nearBoost = exp(-depth * 0.5); // Strong at camera, fades with distance
    vec3 saturatedColor = mix(desatColor, desatColor * 1.0 / max(0.001, (gray / max(0.001, dot(desatColor, vec3(0.299, 0.587, 0.114))))), nearBoost * 0.3);
    
    // Ground plane darkening for nearby areas to add visual texture
    if (uIsGround) {
        float groundDarken = exp(-depth * 0.8) * 0.25; // Darken close ground
        saturatedColor *= (1.0 - groundDarken);
    }

    // Distance fog: brighten/desaturate towards fog color with depth
    float fogT = (1.0 - exp(-uFogDensity * d)) * uFogStrength;
    vec3 foggedColor = mix(saturatedColor, uFogColor, fogT);

    fragColor = vec4(foggedColor, 1.0);
}
</script>

</head>

<body onload="app.start()">
    <main>
        <nav class="breadcrumb" aria-label="Breadcrumb">
            <a href="../index.html">← Zurück zur Übersicht</a>
        </nav>

        <h1>EA7 - Z-Buffer Visualisierung - Pascal Hürten</h1>

        <section id="viewer" aria-labelledby="viewer-heading">
            <h2 id="viewer-heading">Tiefe</h2>

            <div id="canvas-wrap" style="text-align: center; margin: 20px auto;">
                <canvas id="canvas" width="700" height="700"
                    style="border: 1px solid #ccc; border-radius: 4px; max-width:100%; height:auto;"></canvas>
            </div>
            <div id="key-help"
                style="text-align:left; background:#fff; border:1px solid #e6e6e6; padding:12px; border-radius:6px; box-shadow:0 1px 2px rgba(0,0,0,0.04);">
                <div style="display:flex;justify-content:space-between;align-items:center;">
                    <strong>Tastatur-Steuerung</strong>
                </div>
                <ul style="margin:8px 0 0 16px;padding:0;color:#333;line-height:1.4;">
                    <li><strong>w, a, s, d</strong>: Gehen</li>
                    <li><strong>←, →, ↑, ↓</strong>: Kameradrehung</li>
                    <li><strong>m</strong>: Render-Modus umschalten</li>
                </ul>
            </div>
        </section>

        <section id="doc">
            <h2>Dokumentation</h2>

            <h3>Tiefenwirkung durch Z-Buffer und Shader-Effekte</h3>

            <p>In dieser Szene wird eine realistische Tiefenwirkung durch verschiedene, vom Z-Buffer gesteuerte Effekte
                erzielt:</p>

            <div class="shape-section">
                <h4>1. Exponentieller Nebel-Effekt</h4>
                <p>
                    Entfernte Fragmente werden mittels exponentieller Nebel-Funktion in Richtung der Himmelsfarbe
                    aufgehellt und entsättigt.
                    Die Intensität des Effekts basiert auf der View-Space-Tiefe (Z-Koordinate) jedes Fragments:
                </p>
                <div class="math-formula">
                    fogFactor = (1 - exp(-fogDensity × depth)) × fogStrength<br>
                    finalColor = mix(color, fogColor, fogFactor)
                </div>
                <p>
                    Dies simuliert atmosphärische Streuung und vermittelt ein Gefühl für große Distanzen.
                </p>
            </div>

            <div class="shape-section">
                <h4>2. Farbintensität im Vordergrund</h4>
                <p>
                    Objekte in Kameranähe erhalten eine erhöhte Farbsättigung (bis zu 30% mehr), die exponentiell mit
                    der Entfernung abnimmt.
                    Dies macht den Vordergrund lebendiger und "greifbarer":
                </p>
                <div class="math-formula">
                    nearBoost = exp(-depth × 0.5)<br>
                    saturatedColor = mix(desaturated, fullysaturated, nearBoost × 0.3)
                </div>
                <p>
                    Zusätzlich wird der Boden in unmittelbarer Nähe leicht abgedunkelt (bis zu 25%), um visuelle Textur
                    und räumliche Tiefe zu erzeugen.
                </p>
            </div>

            <div class="shape-section">
                <h4>3. Erdkrümmungs-Simulation</h4>
                <p>
                    Im Vertex-Shader werden entfernte Vertices nach unten verschoben, um stilisiert die Krümmung der
                    Erde nachzuahmen.
                    Die Verschiebung basiert auf der Sagitta-Formel für eine Kugeloberfläche:
                </p>
                <div class="math-formula">
                    d = horizontale Distanz (XZ-Ebene)<br>
                    drop = R - sqrt(R² - d²)<br>
                    vertexY -= drop × strength × smoothstep(start, start×2, d)
                </div>
                <p>
                    Der Effekt setzt ab einer konfigurierbaren Startdistanz ein und verstärkt sich quadratisch mit
                    zunehmender Entfernung.
                    Dies erzeugt einen subtilen, aber wahrnehmbaren "Horizont-Effekt", der die Szene größer und weiter
                    wirken lässt.
                </p>
            </div>

            <div class="shape-section">
                <h4>4. Skybox-Atmosphäre</h4>
                <p>
                    Die Skybox erhält im Vertex- und Fragment-Shader einen helleren Farbton zum Horizont hin.
                    Ein höhenbasierter Verlauf (<em>vPosition.y</em> relativ zum Sky-Radius) mischt die Vertexfarbe mit
                    der Nebelfarbe
                    und sorgt für atmosphärische Effekte (Haze und leichte Desättigung) nahe des Horizonts.
                    Dadurch entsteht ein natürlicher Übergang zum Nebeleffekt im fernen Bodenbereich.
                </p>
                <div class="math-formula">
                    h = clamp((y + 0.2·R) / (1.2·R), 0, 1)<br>
                    haze = skyHazeStrength · smoothstep(0, 0.35, 1 - h)<br>
                    finalSkyColor = mix(vertexColor, fogColor, haze)
                </div>
            </div>

        </section>
    </main>
</body>

</html>